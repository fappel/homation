import org.openhab.core.library.types.*
import org.openhab.core.library.items.*
import org.openhab.core.items.*
import org.openhab.core.types.*

import org.eclipse.xtext.xbase.lib.*

import java.lang.*
import java.util.*
import java.util.concurrent.locks.*

var int colorTemperatureSettingRepetitions = 0
val Lock lock = new ReentrantLock()
var Map bulbSwitchStates

val Functions$Function0 initialize = [|
  val Map result = new HashMap()
  ROOMS.members.forEach[ room |
  	val roomBulbs = new HashMap()
  	result.put( room.name, roomBulbs )
    var bulbSwitches = ( room as GroupItem ).members.get( 0 ) as GroupItem
  	bulbSwitches.members.forEach[ bulbSwitch |
      roomBulbs.put( bulbSwitch.name, bulbSwitch.state )
  	]
  ]
  return result
]

val Functions$Function2 mustIlluminate = [ GroupItem room, Map bulbSwitchStates |
  var roomSwitch = room.members.get( 0 ) as GroupItem
  var motion = room.members.get( 3 ) as ContactItem
  var lux = room.members.get( 4 ).state
  var luxThreshold = ( room.members.get( 5 ).state as DecimalType ).intValue()
  var allRoomsTimeout = ( illuminateAllRoomsTimeout.state as DecimalType ).intValue()
  val isOff = newArrayList( true )
  val roomSwitchStates = bulbSwitchStates.get( room.name ) as Map
  roomSwitch.members.forEach[ item |
  	var state = roomSwitchStates.get( item.name ) as OnOffType
    isOff.set( 0, isOff.get( 0 ) && ( state == Uninitialized || state == OFF ) )
  ]
  return    ( activeRoom.state.equals( room.name ) || motion.state == OPEN || allRoomsTimeout > 0 )  
         && isOff.get( 0 ) 
         && ( lux == Uninitialized || lux <= luxThreshold )
]

val Functions$Function2 mustDarken = [ GroupItem room, Map bulbSwitchStates |
  var roomSwitch = room.members.get( 0 ) as GroupItem
  var motion = room.members.get( 3 ) as ContactItem
  var lux = room.members.get( 4 ).state
  var luxThreshold = ( room.members.get( 6 ).state as DecimalType ).intValue()
  var allRoomsTimeout = ( illuminateAllRoomsTimeout.state as DecimalType ).intValue()
  val isOn = newArrayList( false )
  val roomSwitchStates = bulbSwitchStates.get( room.name ) as Map
  roomSwitch.members.forEach[ item |
  	var state = roomSwitchStates.get( item.name ) as OnOffType
  	isOn.set( 0, isOn.get( 0 ) || state == ON )
  ]
  return    !activeRoom.state.equals( room.name ) 
         && motion.state == CLOSED
         && isOn.get( 0 )
         && allRoomsTimeout == 0
         ||    ( lux != Uninitialized && lux > luxThreshold )
            && isOn.get( 0 )
]

val Functions$Function3 bulbSwitchStateProvider = [ GroupItem room, SwitchItem item, OnOffType switchState |
  var scene = sceneSelection.members.get( 0 ) as GroupItem
  var switchExclusionList = scene.members.get( 0 ) as GroupItem
  if( switchExclusionList.members.contains( item ) ) {
  	return OFF
  }
  return switchState
]

val Functions$Function4 switchRoomLight = [ GroupItem room, OnOffType switchState, Map bulbSwitchStates, Functions$Function3 bulbSwitchStateProvider |
  var roomSwitch = room.members.get( 0 ) as GroupItem
  logInfo( "light", "Switch " + roomSwitch.name + " " + switchState )
  val roomSwitches = bulbSwitchStates.get( room.name ) as Map
  roomSwitch.members.forEach[ item |
  	var actualSwitchState = bulbSwitchStateProvider.apply( room, item, switchState ) as OnOffType
    roomSwitches.put( item.name, actualSwitchState )
  ]
  return null
]

val Functions$Function1 applySwitchStates = [ Map bulbSwitchStates |
  ROOMS.members.forEach[ room |
  	val roomSwitches = bulbSwitchStates.get( room.name ) as Map
  	var roomSwitch = ( room as GroupItem ).members.get( 0 ) as GroupItem
  	roomSwitch.members.forEach[ item |
  	  var bulbSwitch = item as SwitchItem
      var state = roomSwitches.get( bulbSwitch.name ) as OnOffType
      if( bulbSwitch.state != state ) {
        logInfo( "light", "  Switch bulb " + bulbSwitch.name + " " + state )
        bulbSwitch.sendCommand( state )
      }
  	]
  ]
  return null
]

val Functions$Function2 brightnessProvider = [ GroupItem room, DimmerItem item |
  var hourOfDay = now.getHourOfDay.intValue()
  if(    room.equals( KITCHEN ) 
  	  && hourOfDay >= ( cookingStart.state as DecimalType ).intValue() 
  	  && hourOfDay < ( cookingEnd.state as DecimalType ).intValue() )
  {
  	return Integer::valueOf( 99 )
  }
  if(    room.equals( BATH_ROOM ) 
  	  && hourOfDay >= ( morningStart.state as DecimalType ).intValue() 
  	  && hourOfDay < ( morningEnd.state as DecimalType ).intValue() )
  {
  	return Integer::valueOf( 99 )
  }
  
  var result = ( brightness.state as DecimalType ).intValue()
  var scene = sceneSelection.members.get( 0 ) as GroupItem
  var brightnessBoost = ( scene.members.get( 2 ).state as DecimalType ).intValue()
  var brightnessBoostThreshold = ( scene.members.get( 3 ).state as DecimalType ).intValue()
  var activity = ( activityRate.state as DecimalType ).intValue()
  if( activity >= brightnessBoostThreshold ) {
  	result = Math::min( result + brightnessBoost, 99 )
  } 
  return result
]

val Functions$Function2 adjustBrightness = [ GroupItem room, Functions$Function2 brightnessProvider |
  if( !activeRoom.state.equals( room.name ) ) { return null }
  var roomSwitch = room.members.get( 0 ) as GroupItem
  var brightnessDimmer = room.members.get( 2 ) as GroupItem
  val isOn = newArrayList()
  roomSwitch.members.forEach[ item | isOn.add( item.state == ON ) ]
  brightnessDimmer.members.forEach[ item |
  	var brightnessValue = ( brightnessProvider.apply( room, item ) as Integer ).intValue()
  	if( isOn.remove( 0 ) == true && ( item.state as DecimalType ).intValue() != brightnessValue  ) {
      logInfo( "light", "  set brightness of " + item.name + " to " + brightnessValue )
      var PercentType percentage = new PercentType( brightnessValue ) 
      item.sendCommand( percentage )
  	} 
  ]
  return null
]

val Functions$Function2 colorTemperatureProvider = [ GroupItem room, DimmerItem item |
  return ( colorTemperature.state as DecimalType ).intValue()
]

val Functions$Function3 adjustColorTemperature = [ GroupItem room, Functions$Function2 colorTemperatureProvider, boolean force |
  if( !activeRoom.state.equals( room.name ) ) { return null }
  var roomSwitch = room.members.get( 0 ) as GroupItem
  var temperatureDimmer = room.members.get( 1 ) as GroupItem
  val isOn = newArrayList()
  roomSwitch.members.forEach[ item | isOn.add( item.state == ON ) ]
  temperatureDimmer.members.forEach[ item |
    var int temperatureValue = ( colorTemperatureProvider.apply( room, item ) as Integer ).intValue()
    var switchedOn = isOn.remove( 0 ) == true
    if( switchedOn && force || switchedOn && ( item.state as DecimalType ).intValue() != temperatureValue ) {
      logInfo( "light", "  set color temperature of " + item.name + " to " + temperatureValue )
      var PercentType percentage = new PercentType( temperatureValue ) 
      item.sendCommand( percentage )
  	}
  ]
  return null
]

val Functions$Function0 switchOffActiveRoomLightsOnTimeout = [|
  AUTO_SWITCH_OFF.members.forEach[ item |
  	var GroupItem roomAutoSwitch = item as GroupItem
  	var room = roomAutoSwitch.members.get( 0 ) as GroupItem
  	var motion = roomAutoSwitch.members.get( 1 ) as ContactItem
  	var autoSwitch = roomAutoSwitch.members.get( 2 ) as SwitchItem
  	var timeoutDefinition = ( roomAutoSwitch.members.get( 3 ).state as DecimalType ).intValue()
  	var timeoutValue = ( roomAutoSwitch.members.get( 4 ).state as DecimalType ).intValue()
  	var newTimeoutValue = 0
    if( activeRoom.state.equals( room.name ) && motion.state == CLOSED && autoSwitch.state == ON ) {
      if( timeoutValue >= timeoutDefinition ) {
        postUpdate( activeRoom, "NONE" )
        if( !sceneSelection.members.get( 0 ).equals( IDLE_PERIOD ) ) {
          postUpdate( activeScene, IDLE_PERIOD.name )
        }
      } else {
        newTimeoutValue = timeoutValue + 2
      }
    }
    if( timeoutValue != newTimeoutValue ) {
      postUpdate( roomAutoSwitch.members.get( 4 ), newTimeoutValue )
    }
  ]
  return null  
]

val Functions$Function1 resetSwitchOffActiveRoomLightTimeout = [ GroupItem room |
  if( ( room.members.get( 4 ).state as DecimalType ).intValue() != 0 ) {
    postUpdate( room.members.get( 4 ), 0 )
  } 
]

val Functions$Function0 updateIlluminateAllRoomsTimeout = [|
  if( ( illuminateAllRoomsTimeout.state as DecimalType ).intValue() > 0 ) {
    postUpdate( illuminateAllRoomsTimeout, ( illuminateAllRoomsTimeout.state as DecimalType ).intValue() -1 )
  }
  return null
]

val Functions$Function0 initializeIlluminateAllRoomsTimeout = [|
  var scene = ( sceneSelection.members.get( 0 ) as GroupItem )
  var timeout = ( scene.members.get( 1 ).state as DecimalType ).intValue()
  postUpdate( illuminateAllRoomsTimeout, timeout )
  return null
]

rule "Wait for Initialization"
when
  System started
then
  lock.lock
  try {
  	bulbSwitchStates = initialize.apply() as Map
    postUpdate( lightRulesReady, ON )
    logInfo( "light", "Light rules wait for rule system initialization..." )
  } finally {
    lock.unlock	
  }
end

rule "Room Activation"
when
  Item activeRoom changed
then
  if( initialized.state == ON ) {
    lock.lock
    try {
      logInfo( "light", "Activate room " + activeRoom.state )

      ROOMS.members.forEach[ room |
        if( mustIlluminate.apply( room, bulbSwitchStates ) as Boolean ) {
          initializeIlluminateAllRoomsTimeout.apply()
          switchRoomLight.apply( room, ON, bulbSwitchStates, bulbSwitchStateProvider )
          applySwitchStates.apply( bulbSwitchStates )
          colorTemperatureSettingRepetitions = 2
        }
      ]

      AUTO_SWITCH_OFF.members.forEach[ room | resetSwitchOffActiveRoomLightTimeout.apply( room ) ]
    } finally {
      lock.unlock	
    }
  }
end

rule "Periodical Bulb Updates"
when
  Time cron "0/2 * * * * ?"
then
  if( initialized.state == ON ) {
    lock.lock
    try {
      ROOMS.members.forEach[ room |
        if( mustIlluminate.apply( room, bulbSwitchStates ) as Boolean ) {
          switchRoomLight.apply( room, ON, bulbSwitchStates, bulbSwitchStateProvider )
          colorTemperatureSettingRepetitions = 2
        }
      ]
      ROOMS.members.forEach[ room |
        if( mustDarken.apply( room, bulbSwitchStates ) as Boolean ) {
          switchRoomLight.apply( room, OFF, bulbSwitchStates, bulbSwitchStateProvider )
        }
      ]
      applySwitchStates.apply( bulbSwitchStates )

      if( brightness.state != Undefined ) {
        ROOMS.members.forEach[ room | adjustBrightness.apply( room, brightnessProvider ) ]
      }
      if( colorTemperature.state != Undefined ) {
        val force = colorTemperatureSettingRepetitions > 0
        ROOMS.members.forEach[ room | adjustColorTemperature.apply( room, colorTemperatureProvider, force ) ]
        colorTemperatureSettingRepetitions = Math::max( 0, colorTemperatureSettingRepetitions - 1 )
      }

      switchOffActiveRoomLightsOnTimeout.apply()
      updateIlluminateAllRoomsTimeout.apply()    
    } finally {
      lock.unlock	
    }
  }
end

rule "Brightness Calculation"
when
  Item sunHeight changed
then
  if( initialized.state == ON ) {
    lock.lock
    try {
      if( autoBrightness.state == ON ) {
      	var minimum = ( minimumBrightness.state as DecimalType ).intValue()
        var factor = Math::min( 3.33, Math::max( 2.0, ( Math::abs( ( 10 + now.getDayOfYear ) % 366 - 183 ) / 51.85 ) ) )
        var value = Math::max( minimum, Math::min( ( ( ( sunHeight.state as DecimalType ).intValue() + 18 ) * factor ), 99.0 ) ).intValue()
        if( brightness.state != value ) {
          logInfo( "light", "Update Brightness: " + value )
          postUpdate( brightness, value )
        } 
      }
    } finally {
      lock.unlock	
    }
  }
end

rule "Brightness Adjustment"
when
  Item brightness changed
then
  if( initialized.state == ON ) {
    lock.lock
    try {
      ROOMS.members.forEach[ room | adjustBrightness.apply( room, brightnessProvider ) ]
    } finally {
      lock.unlock	
    }
  }
end

rule "Color Temperature Calculation"
when
  Item sunHeight changed
then
  if( initialized.state == ON ) {
    lock.lock
    try {
      if( autoColorTemperature.state == ON ) {
        var factor = Math::max( 2.0, ( Math::abs( ( 10 + now.getDayOfYear ) % 366 - 183 ) / 31 ) )
        var value = Math::min( ( ( ( sunHeight.state as DecimalType ).intValue() + 7 ) * factor ), 100.0 )
        var temperature = ( 100.0 - Math::max( 0.0, value ) ).intValue()
        if( colorTemperature.state != temperature ) {  	
          logInfo( "light", "Update Color Temperature: " + temperature )
          postUpdate( colorTemperature, temperature )
        }
      }
    } finally {
      lock.unlock	
    }
  }
end

rule "Color Temperature Adjustment"
when
  Item colorTemperature changed
then
  if( initialized.state == ON ) {
    lock.lock
    try {
      ROOMS.members.forEach[ room | adjustColorTemperature.apply( room, colorTemperatureProvider, true ) ]
    } finally {
      lock.unlock
    }
  }
end

rule "Scene Activation"
when
  Item activeScene changed
then
  if( initialized.state == ON ) {
    lock.lock
    try {
      logInfo( "light", "Switch bulbs for scene " + activeScene.state )

      ROOMS.members.forEach[ room |
        switchRoomLight.apply( room, OFF, bulbSwitchStates, bulbSwitchStateProvider )
      ]
    } finally {
      lock.unlock	
    }
  }
end